buildscript {
    ext {
        lombok = '1.18.20'
        apachPoi = '4.1.2'
        javax = '2.0.1.Final'
    }
}

plugins {
    id "java-library"
    id "maven-publish"
    id "signing"
    id "com.github.neherim.quality.check" version "0.1.8"
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        //testImplementation "bad.robot:simple-excel:1.2"
        url = "https://robotooling.com/maven/"
    }
}

group = 'voofai.lib'
version = '0.1.1'
sourceCompatibility = 1.8

qualityCheck {
    checkstyle {
        configFile = rootProject.file('config/checkstyle/checkstyle.xml')
    }
    pmd {
        enabled = false
        ruleSetFile = rootProject.file('config/pmd/pmd.xml')
    }
    spotbugs {
        enabled = false
        reportFormat = "html"
    }
    pitest {
        enabled = false
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

test {
    useJUnitPlatform()
}

dependencies {

    //MS Excel/Word
    implementation "org.apache.poi:poi-ooxml:$apachPoi"
    implementation "org.apache.poi:poi-ooxml-schemas:$apachPoi"
    implementation "org.apache.poi:poi:$apachPoi"

    //annotation
    annotationProcessor "org.projectlombok:lombok:$lombok"
    compileOnly "org.projectlombok:lombok:$lombok"
    implementation "javax.validation:validation-api:$javax"

    //test
    testImplementation "org.junit.jupiter:junit-jupiter:5.7.0"
    testImplementation "ch.qos.logback:logback-classic:1.2.3"
    testImplementation "org.slf4j:slf4j-api:1.7.30"

    //test - compare Excel Workbook
    testImplementation "bad.robot:simple-excel:1.2"
}

publishing {
    repositories {
        maven {
            name = "maven_central_package"
            def releasesRepoUrl = uri('https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/')
            def snapshotsRepoUrl = uri('https://s01.oss.sonatype.org/content/repositories/snapshots/')
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = "$usr"
                password = "$pwd"
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            groupId 'io.github.voofai'
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'apach-poi-reports'
                description = 'Excell & Word reports generator'
                url = 'https://github.com/VooFai/apach-poi-reports'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/VooFai/apach-poi-reports/blob/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'voofai'
                        name = 'voofai'
                        //email = 'john.doe@example.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:VooFai/apach-poi-reports.git'
                    developerConnection = 'scm:git:git@github.com:VooFai/apach-poi-reports.git'
                    url = 'https://github.com/VooFai/apach-poi-reports'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}